import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java-library'
    id 'idea'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.6'
}

repositories {
    mavenCentral()
    maven { url "https://maven.bookmap.com/maven2/releases/" }
}

dependencies {
    def commonDeps = [
            'com.bookmap.api:api-core:7.4.0.19',
            'com.bookmap.api:api-simplified:7.4.0.19',
            'org.apache.commons:commons-lang3:3.11',
            'net.java.dev.jna:jna:5.13.0' // Add the JNA library dependency here

    ]
    if (findProperty('is_built_from_main_bookmap_project')) {
        implementation fileTree(dir: "${main_libs}", include: ['*.jar'])
        implementation project(':Level1Api')
        implementation project(':SimplifiedApiWrapper')
    } else {
        implementation commonDeps
    }
}

jar {
    archiveFileName = "Bookmap_Add_On_${LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyy_M_d_HH_mm"))}.jar"
}

idea.module.downloadJavadoc = true

if (idea.project) {
    idea.project.settings.runConfigurations {
        def addOpensOptions = ([
                'java.base/java.lang',
                'java.base/java.io',
                'java.base/java.math',
                'java.base/java.util',
                'java.base/java.util.concurrent',
                'java.base/java.net',
                'java.base/java.text',
                'java.desktop/java.awt',
                'java.desktop/java.awt.color',
                'java.desktop/java.awt.peer',
                'java.desktop/com.sun.java.swing',
                'java.prefs/java.util.prefs'
        ] as List).collect { "--add-opens=$it=ALL-UNNAMED" }.join(' ')
        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            addOpensOptions += ' --add-opens=java.desktop/sun.awt.windows=ALL-UNNAMED'
        }

        'BookmapJar'(org.jetbrains.gradle.ext.JarApplication) {
            jarPath = 'C:\\Program Files\\Bookmap\\Bookmap.jar'
            workingDirectory = 'C:\\Bookmap'
            jvmArgs = addOpensOptions
        }
    }
}
